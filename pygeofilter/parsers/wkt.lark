?geometry: point
         | linestring
         | polygon
         | multipoint
         | multilinestring
         | multipolygon
         | geometrycollection

geometrycollection: "GEOMETRYCOLLECTION" "(" geometry ( "," geometry )* ")"
point: "POINT" "(" coordinate ")"
linestring: "LINESTRING" "(" coordinate_list ")"
polygon: "POLYGON" "(" coordinate_lists ")"

multipoint: "MULTIPOINT" "(" coordinate_list ")"
          | "MULTIPOINT" "(" "(" coordinate ")" ( "," "(" coordinate ")" )* ")"
multilinestring: "MULTILINESTRING" "(" coordinate_lists ")"




// TODO
multipolygon: "MULTIPOLYGON" "(" "(" coordinate_lists ")" ( "," "(" coordinate_lists ")" )* ")"

coordinate_lists: "(" coordinate_list ")" ( "," "(" coordinate_list ")" )*


// TODO: multi- versions
// MULTIPOINT ((10 40), (40 30), (20 20), (30 10))
// MULTIPOINT (10 40, 40 30, 20 20, 30 10)

// MULTILINESTRING ((10 10, 20 20, 10 40),
// (40 40, 30 30, 40 20, 30 10))

// MULTIPOLYGON (((40 40, 20 45, 45 30, 40 40)),
// ((20 35, 10 30, 10 10, 30 5, 45 20, 20 35),
// (30 20, 20 15, 20 25, 30 20)))

?coordinate_list: coordinate_list "," coordinate
                | coordinate                                            -> coordinate_list_start
coordinate: NUMBER NUMBER [ NUMBER [ NUMBER ] ]

%import common.NUMBER